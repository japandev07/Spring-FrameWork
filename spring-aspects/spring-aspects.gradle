description = "Spring Aspects"

apply plugin: "io.freefair.aspectj"

sourceSets.main.aspectj.srcDir "src/main/java"
sourceSets.main.java.srcDirs = files()

sourceSets.test.aspectj.srcDir "src/test/java"
sourceSets.test.java.srcDirs = files()

aspectj.version = aspectjVersion

dependencies {
	aspect(project(":spring-orm"))
	compile("org.aspectj:aspectjweaver:${aspectjVersion}")
	compileOnly("org.aspectj:aspectjrt:${aspectjVersion}")
	optional(project(":spring-aop"))  // for @Async support
	optional(project(":spring-beans"))  // for @Configurable support
	optional(project(":spring-context"))  // for @Enable* support
	optional(project(":spring-context-support"))  // for JavaMail and JSR-107 support
	optional(project(":spring-orm"))  // for JPA exception translation support
	optional(project(":spring-tx"))  // for JPA, @Transactional support
	optional("javax.cache:cache-api:1.1.0") // for JCache aspect
	optional("javax.transaction:javax.transaction-api:1.3")  // for @javax.transaction.Transactional support
	testCompile(project(":spring-core"))  // for CodeStyleAspect
	testCompile(project(":spring-test"))
	testCompile("javax.mail:javax.mail-api:1.6.2")
	testCompileOnly("org.aspectj:aspectjrt:${aspectjVersion}")
}

eclipse.project {
	natures += "org.eclipse.ajdt.ui.ajnature"
	buildCommands = [new org.gradle.plugins.ide.eclipse.model.BuildCommand("org.eclipse.ajdt.core.ajbuilder")]
}
